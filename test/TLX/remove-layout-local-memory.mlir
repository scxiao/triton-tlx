// RUN: triton-opt %s -split-input-file -tritongpu-remove-layout-conversions | FileCheck %s

// Test that redundant layout conversion after local_load is removed

// CHECK: #[[$COALESCED:.*]] = #ttg.blocked<{sizePerThread = [1, 8], threadsPerWarp = [4, 8], warpsPerCTA = [4, 1], order = [1, 0]}>
// CHECK-LABEL: @local_load_coalesce
// CHECK: ttg.local_load %{{.*}} -> tensor<128x64xf16, #[[$COALESCED]]>
// CHECK-NOT: ttg.convert_layout
// CHECK: ttg.local_store

#blocked = #ttg.blocked<{sizePerThread = [1, 1], threadsPerWarp = [32, 1], warpsPerCTA = [4, 1], order = [0, 1]}>
#blocked1 = #ttg.blocked<{sizePerThread = [1, 8], threadsPerWarp = [4, 8], warpsPerCTA = [4, 1], order = [1, 0]}>
#shared = #ttg.swizzled_shared<{vec = 1, perPhase = 1, maxPhase = 1, order = [1, 0]}>
#smem = #ttg.shared_memory
module attributes {"ttg.num-ctas" = 1 : i32, "ttg.num-warps" = 4 : i32, "ttg.threads-per-warp" = 32 : i32} {
tt.func @local_load_coalesce(%arg0: !ttg.memdesc<128x64xf16, #shared, #smem>, %arg1: !ttg.memdesc<128x64xf16, #shared, #smem, mutable>) {
  %0 = ttg.local_load %arg0 : !ttg.memdesc<128x64xf16, #shared, #smem> -> tensor<128x64xf16, #blocked1>
  %1 = ttg.convert_layout %0 : tensor<128x64xf16, #blocked1> -> tensor<128x64xf16, #blocked>
  ttg.local_store %1, %arg1 : tensor<128x64xf16, #blocked> -> !ttg.memdesc<128x64xf16, #shared, #smem, mutable>
  tt.return
}
}

// -----

// Test that a store-only conversion through a non-reordering reshape is pruned:
//
//   local_store(reshape(convert_layout(x)), dst)
//
// becomes:
//
//   local_store(reshape(x), dst)
//
// CHECK-LABEL: @local_store_reshape_convert
// CHECK-SAME: %[[ARG:.*]]: tensor<32x4x32xf32, #[[$SRC:[^>]+]]>
// CHECK-NOT: ttg.convert_layout
// CHECK: %[[RESHAPE:.*]] = tt.reshape %[[ARG]] : tensor<32x4x32xf32, #[[$SRC]]> -> tensor<32x128xf32, #[[$DIRECT:.*]]>
// CHECK-NEXT: ttg.local_store %[[RESHAPE]], %{{.*}} {async_task_id = array<i32: 7>, loop.cluster = 2 : i32, loop.stage = 3 : i32} : tensor<32x128xf32, #[[$DIRECT]]> -> !ttg.memdesc<32x128xf32, #{{.*}}, #{{.*}}, mutable>
// CHECK-NEXT: tt.return

#linear_src = #ttg.linear<{register = [[1, 0, 0], [0, 0, 8], [8, 0, 0], [16, 0, 0], [0, 0, 16]], lane = [[2, 0, 0], [4, 0, 0], [0, 0, 1], [0, 0, 2], [0, 0, 4]], warp = [[0, 1, 0], [0, 2, 0]], block = []}>
#linear_cvt = #ttg.linear<{register = [[1, 0, 0], [8, 0, 0], [16, 0, 0], [0, 0, 1], [0, 0, 2], [0, 0, 4], [0, 0, 8], [0, 0, 16]], lane = [[2, 0, 0], [4, 0, 0], [0, 0, 0], [0, 0, 0], [0, 0, 0]], warp = [[0, 1, 0], [0, 2, 0]], block = []}>
#linear_cvt_flat = #ttg.linear<{register = [[1, 0], [8, 0], [16, 0], [0, 1], [0, 2], [0, 4], [0, 8], [0, 16]], lane = [[2, 0], [4, 0], [0, 0], [0, 0], [0, 0]], warp = [[0, 32], [0, 64]], block = []}>
#shared_store = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = false, elementBitWidth = 32}>
#smem_store = #ttg.shared_memory
module attributes {"ttg.num-ctas" = 1 : i32, "ttg.num-warps" = 4 : i32, "ttg.threads-per-warp" = 32 : i32, ttg.target = "cuda:100"} {
  tt.func @local_store_reshape_convert(
      %arg0: tensor<32x4x32xf32, #linear_src>,
      %arg1: !ttg.memdesc<32x128xf32, #shared_store, #smem_store, mutable>) {
    %cvt = ttg.convert_layout %arg0 : tensor<32x4x32xf32, #linear_src> -> tensor<32x4x32xf32, #linear_cvt>
    %reshape = tt.reshape %cvt : tensor<32x4x32xf32, #linear_cvt> -> tensor<32x128xf32, #linear_cvt_flat>
    ttg.local_store %reshape, %arg1 {async_task_id = array<i32: 7>, loop.cluster = 2 : i32, loop.stage = 3 : i32} : tensor<32x128xf32, #linear_cvt_flat> -> !ttg.memdesc<32x128xf32, #shared_store, #smem_store, mutable>
    tt.return
  }
}

// -----

// Test layout conflict resolution when both tmem_load and local_load are in the
// same kernel with different layouts. The pass should prefer TMEM's layout with
// larger sizePerThread ([1, 128], score=128) for better memory access efficiency.
//
// After the pass, the larger layout ([1, 128]) should be selected for both loads,
// eliminating the need for intermediate convert_layout ops.

// CHECK: #[[$TMEM_LAYOUT:.*]] = #ttg.blocked<{sizePerThread = [1, 128], threadsPerWarp = [32, 1], warpsPerCTA = [4, 1], order = [0, 1]}>
// CHECK-LABEL: @tmem_and_local_load_conflict_resolution
// Both loads should use the TMEM layout with higher score [1, 128]
// CHECK: ttng.tmem_load %{{.*}} -> tensor<128x128xf32, #[[$TMEM_LAYOUT]]>
// CHECK: ttg.local_load %{{.*}} -> tensor<128x128xbf16, #[[$TMEM_LAYOUT]]>
// The convert_layout to the original common layout should still exist at the end
// CHECK: ttg.convert_layout

#blocked_tmem = #ttg.blocked<{sizePerThread = [1, 128], threadsPerWarp = [32, 1], warpsPerCTA = [4, 1], order = [0, 1]}>
#blocked_common = #ttg.blocked<{sizePerThread = [1, 1], threadsPerWarp = [1, 32], warpsPerCTA = [1, 4], order = [1, 0]}>
#blocked_smem = #ttg.blocked<{sizePerThread = [1, 8], threadsPerWarp = [2, 16], warpsPerCTA = [4, 1], order = [1, 0]}>
#tmem = #ttng.tensor_memory_encoding<blockM = 128, blockN = 128, colStride = 1>
#shared1 = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = false, elementBitWidth = 16}>
#smem1 = #ttg.shared_memory
module attributes {"ttg.num-ctas" = 1 : i32, "ttg.num-warps" = 4 : i32, "ttg.threads-per-warp" = 32 : i32, ttg.target = "cuda:100"} {
tt.func @tmem_and_local_load_conflict_resolution(
    %tmem_buf: !ttg.memdesc<128x128xf32, #tmem, #ttng.tensor_memory, mutable>,
    %smem_buf: !ttg.memdesc<128x128xbf16, #shared1, #smem1>) -> tensor<128x128xf32, #blocked_common> {
  // TMEM load with large sizePerThread [1, 128], score = 128
  %result = ttng.tmem_load %tmem_buf : !ttg.memdesc<128x128xf32, #tmem, #ttng.tensor_memory, mutable> -> tensor<128x128xf32, #blocked_tmem>
  %result_cvt = ttg.convert_layout %result : tensor<128x128xf32, #blocked_tmem> -> tensor<128x128xf32, #blocked_common>
  // SMEM local_load with small sizePerThread [1, 8], score = 8
  %y = ttg.local_load %smem_buf : !ttg.memdesc<128x128xbf16, #shared1, #smem1> -> tensor<128x128xbf16, #blocked_smem>
  %y_cvt = ttg.convert_layout %y : tensor<128x128xbf16, #blocked_smem> -> tensor<128x128xbf16, #blocked_common>
  // Add them together (requires same layout)
  %y_ext = arith.extf %y_cvt : tensor<128x128xbf16, #blocked_common> to tensor<128x128xf32, #blocked_common>
  %z = arith.addf %result_cvt, %y_ext : tensor<128x128xf32, #blocked_common>
  tt.return %z : tensor<128x128xf32, #blocked_common>
}
}

// -----

// Test that tmem_load's linear layout takes priority over local_load's blocked
// layout. tmem_load produces a hardware-fixed linear layout that cannot be
// changed, while local_load can adapt to any layout. Preferring the linear
// layout avoids a convert_layout that would consume shared memory.

// CHECK: #[[$LINEAR:.*]] = #ttg.linear
// CHECK-LABEL: @tmem_linear_layout_priority
// CHECK: ttng.tmem_load {{.*}} -> tensor<64x128xf32, #[[$LINEAR]]>
// CHECK: ttg.local_load {{.*}} -> tensor<64x128xbf16, #[[$LINEAR]]>
// CHECK-NOT: ttg.convert_layout
// CHECK: arith.addf {{.*}} : tensor<64x128xf32, #[[$LINEAR]]>
// CHECK: ttg.local_store

#linear_tmem = #ttg.linear<{register = [[0, 1], [0, 2], [0, 4], [0, 8], [0, 16]], lane = [[1, 0], [2, 0], [4, 0], [8, 0], [0, 32]], warp = [[16, 0], [32, 0], [0, 64]], block = []}>
#blocked_smem2 = #ttg.blocked<{sizePerThread = [1, 8], threadsPerWarp = [2, 16], warpsPerCTA = [8, 1], order = [1, 0]}>
#shared_nv = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = false, elementBitWidth = 16}>
#smem2 = #ttg.shared_memory
#tmem2 = #ttng.tensor_memory_encoding<blockM = 64, blockN = 128, colStride = 1>
module attributes {"ttg.num-ctas" = 1 : i32, "ttg.num-warps" = 8 : i32, ttg.target = "cuda:100", "ttg.threads-per-warp" = 32 : i32} {
  tt.func public @tmem_linear_layout_priority(%arg_o: !ttg.memdesc<64x128xf32, #tmem2, #ttng.tensor_memory, mutable>, %arg_res: !ttg.memdesc<64x128xbf16, #shared_nv, #smem2, mutable>, %arg_out: !ttg.memdesc<64x128xbf16, #shared_nv, #smem2, mutable>) {
    %cst_eps = arith.constant dense<9.99999974E-6> : tensor<64x1xf32, #linear_tmem>
    %o = ttng.tmem_load %arg_o : !ttg.memdesc<64x128xf32, #tmem2, #ttng.tensor_memory, mutable> -> tensor<64x128xf32, #linear_tmem>
    %sq = arith.mulf %o, %o : tensor<64x128xf32, #linear_tmem>
    %sum = "tt.reduce"(%sq) <{axis = 1 : i32}> ({
    ^bb0(%a: f32, %b: f32):
      %s = arith.addf %a, %b : f32
      tt.reduce.return %s : f32
    }) : (tensor<64x128xf32, #linear_tmem>) -> tensor<64xf32, #ttg.slice<{dim = 1, parent = #linear_tmem}>>
    %sum_exp = tt.expand_dims %sum {axis = 1 : i32} : tensor<64xf32, #ttg.slice<{dim = 1, parent = #linear_tmem}>> -> tensor<64x1xf32, #linear_tmem>
    %sum_eps = arith.addf %sum_exp, %cst_eps : tensor<64x1xf32, #linear_tmem>
    %rrms = tt.extern_elementwise %sum_eps {libname = "", libpath = "", pure = true, symbol = "__nv_rsqrtf"} : (tensor<64x1xf32, #linear_tmem>) -> tensor<64x1xf32, #linear_tmem>
    %rrms_bcast = tt.broadcast %rrms : tensor<64x1xf32, #linear_tmem> -> tensor<64x128xf32, #linear_tmem>
    %result = arith.mulf %o, %rrms_bcast : tensor<64x128xf32, #linear_tmem>
    %result_cvt = ttg.convert_layout %result : tensor<64x128xf32, #linear_tmem> -> tensor<64x128xf32, #blocked_smem2>
    %res = ttg.local_load %arg_res : !ttg.memdesc<64x128xbf16, #shared_nv, #smem2, mutable> -> tensor<64x128xbf16, #blocked_smem2>
    %res_f32 = arith.extf %res : tensor<64x128xbf16, #blocked_smem2> to tensor<64x128xf32, #blocked_smem2>
    %add = arith.addf %result_cvt, %res_f32 : tensor<64x128xf32, #blocked_smem2>
    %out = arith.truncf %add : tensor<64x128xf32, #blocked_smem2> to tensor<64x128xbf16, #blocked_smem2>
    ttg.local_store %out, %arg_out : tensor<64x128xbf16, #blocked_smem2> -> !ttg.memdesc<64x128xbf16, #shared_nv, #smem2, mutable>
    tt.return
  }
}

// -----

// A terminal local_store is layout-flexible, so keep an AMD MMA producer in
// its native layout through an elementwise epilogue instead of round-tripping
// through shared memory for a convert_layout.

// CHECK-LABEL: @keep_amd_wmma_for_local_store
// CHECK: %[[WMMA_DOT:.*]] = tt.dot
// CHECK-NOT: ttg.convert_layout
// CHECK: %[[WMMA_TRUNC:.*]] = arith.truncf %[[WMMA_DOT]] : tensor<128x128xf32, #[[$WMMA:.*]]> to tensor<128x128xf16, #[[$WMMA]]>
// CHECK-NEXT: ttg.local_store %[[WMMA_TRUNC]], {{.*}} : tensor<128x128xf16, #[[$WMMA]]>

#blocked_wmma_out = #ttg.blocked<{sizePerThread = [4, 4], threadsPerWarp = [1, 32], warpsPerCTA = [4, 1], order = [1, 0]}>
#wmma_out = #ttg.amd_wmma<{version = 3, isTranspose = true, ctaLayout = {warp = [[0, 1], [1, 0]]}, instrShape = [16, 16, 32]}>
#wmma_lhs = #ttg.dot_op<{opIdx = 0, parent = #wmma_out, kWidth = 8}>
#wmma_rhs = #ttg.dot_op<{opIdx = 1, parent = #wmma_out, kWidth = 8}>
#shared_wmma_out = #ttg.padded_shared<[128:+8] {order = [1, 0], shape = [128, 128]}>
#smem_wmma_out = #ttg.shared_memory

module attributes {"ttg.num-ctas" = 1 : i32, "ttg.num-warps" = 4 : i32, ttg.target = "hip:gfx1250", "ttg.threads-per-warp" = 32 : i32} {
  tt.func @keep_amd_wmma_for_local_store(
      %a: tensor<128x32xf16, #wmma_lhs>,
      %b: tensor<32x128xf16, #wmma_rhs>,
      %acc: tensor<128x128xf32, #wmma_out>,
      %dst: !ttg.memdesc<128x128xf16, #shared_wmma_out, #smem_wmma_out, mutable>) {
    %dot = tt.dot %a, %b, %acc : tensor<128x32xf16, #wmma_lhs> * tensor<32x128xf16, #wmma_rhs> -> tensor<128x128xf32, #wmma_out>
    %converted = ttg.convert_layout %dot : tensor<128x128xf32, #wmma_out> -> tensor<128x128xf32, #blocked_wmma_out>
    %truncated = arith.truncf %converted : tensor<128x128xf32, #blocked_wmma_out> to tensor<128x128xf16, #blocked_wmma_out>
    ttg.local_store %truncated, %dst : tensor<128x128xf16, #blocked_wmma_out> -> !ttg.memdesc<128x128xf16, #shared_wmma_out, #smem_wmma_out, mutable>
    tt.return
  }
}

// -----

// CHECK-LABEL: @keep_amd_mfma_for_local_store
// CHECK: %[[MFMA_STORE_DOT:.*]] = tt.dot
// CHECK-NOT: ttg.convert_layout
// CHECK: %[[MFMA_STORE_TRUNC:.*]] = arith.truncf %[[MFMA_STORE_DOT]] : tensor<64x64xf32, #[[$MFMA_STORE:.*]]> to tensor<64x64xf16, #[[$MFMA_STORE]]>
// CHECK-NEXT: ttg.local_store %[[MFMA_STORE_TRUNC]], {{.*}} : tensor<64x64xf16, #[[$MFMA_STORE]]>
// CHECK-LABEL: @keep_amd_mfma_for_initialized_local_alloc
// CHECK: %[[MFMA_ALLOC_DOT:.*]] = tt.dot
// CHECK-NOT: ttg.convert_layout
// CHECK: %[[MFMA_ALLOC_TRUNC:.*]] = arith.truncf %[[MFMA_ALLOC_DOT]] : tensor<64x64xf32, #[[$MFMA_ALLOC:.*]]> to tensor<64x64xf16, #[[$MFMA_ALLOC]]>
// CHECK-NEXT: ttg.local_alloc %[[MFMA_ALLOC_TRUNC]] : (tensor<64x64xf16, #[[$MFMA_ALLOC]]>)

#blocked_mfma_out = #ttg.blocked<{sizePerThread = [4, 4], threadsPerWarp = [4, 16], warpsPerCTA = [2, 2], order = [1, 0]}>
#mfma_out = #ttg.amd_mfma<{version = 3, warpsPerCTA = [2, 2], instrShape = [32, 32, 8], isTransposed = true}>
#mfma_lhs = #ttg.dot_op<{opIdx = 0, parent = #mfma_out, kWidth = 4}>
#mfma_rhs = #ttg.dot_op<{opIdx = 1, parent = #mfma_out, kWidth = 4}>
#shared_mfma_out = #ttg.swizzled_shared<{vec = 1, perPhase = 1, maxPhase = 1, order = [1, 0]}>
#smem_mfma_out = #ttg.shared_memory

module attributes {"ttg.num-ctas" = 1 : i32, "ttg.num-warps" = 4 : i32, ttg.target = "hip:gfx942", "ttg.threads-per-warp" = 64 : i32} {
  tt.func @keep_amd_mfma_for_local_store(
      %a: tensor<64x32xf16, #mfma_lhs>,
      %b: tensor<32x64xf16, #mfma_rhs>,
      %acc: tensor<64x64xf32, #mfma_out>,
      %dst: !ttg.memdesc<64x64xf16, #shared_mfma_out, #smem_mfma_out, mutable>) {
    %dot = tt.dot %a, %b, %acc : tensor<64x32xf16, #mfma_lhs> * tensor<32x64xf16, #mfma_rhs> -> tensor<64x64xf32, #mfma_out>
    %converted = ttg.convert_layout %dot : tensor<64x64xf32, #mfma_out> -> tensor<64x64xf32, #blocked_mfma_out>
    %truncated = arith.truncf %converted : tensor<64x64xf32, #blocked_mfma_out> to tensor<64x64xf16, #blocked_mfma_out>
    ttg.local_store %truncated, %dst : tensor<64x64xf16, #blocked_mfma_out> -> !ttg.memdesc<64x64xf16, #shared_mfma_out, #smem_mfma_out, mutable>
    tt.return
  }

  tt.func @keep_amd_mfma_for_initialized_local_alloc(
      %a: tensor<64x32xf16, #mfma_lhs>,
      %b: tensor<32x64xf16, #mfma_rhs>,
      %acc: tensor<64x64xf32, #mfma_out>) -> !ttg.memdesc<64x64xf16, #shared_mfma_out, #smem_mfma_out, mutable> {
    %dot = tt.dot %a, %b, %acc : tensor<64x32xf16, #mfma_lhs> * tensor<32x64xf16, #mfma_rhs> -> tensor<64x64xf32, #mfma_out>
    %converted = ttg.convert_layout %dot : tensor<64x64xf32, #mfma_out> -> tensor<64x64xf32, #blocked_mfma_out>
    %truncated = arith.truncf %converted : tensor<64x64xf32, #blocked_mfma_out> to tensor<64x64xf16, #blocked_mfma_out>
    %initialized = ttg.local_alloc %truncated : (tensor<64x64xf16, #blocked_mfma_out>) -> !ttg.memdesc<64x64xf16, #shared_mfma_out, #smem_mfma_out, mutable>
    tt.return %initialized : !ttg.memdesc<64x64xf16, #shared_mfma_out, #smem_mfma_out, mutable>
  }
}

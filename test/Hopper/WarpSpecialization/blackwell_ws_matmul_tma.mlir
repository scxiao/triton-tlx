// RUN: triton-opt %s -split-input-file --nvgpu-warp-specialization="num-stages=3 capability=100 smem-budget=200000" | FileCheck %s

// Test case: Basic Blackwell matrix multiplication with TMA and warp specialization.
// This IR represents a GEMM kernel that uses tensor memory for accumulator
// and has partition annotations on key operations.

// CHECK-LABEL: @matmul_kernel_tma_ws
// CHECK: ttg.barrier local
// CHECK-NOT: ttg.barrier local
// CHECK: ttg.warp_specialize
// Default group: MMA operations
// CHECK: default
// CHECK: ttng.wait_barrier {{.*}}channelGraph = array<i32: {{.*}}direction = "forward"{{.*}}dstTask = {{[0-9]+}} : i32{{.*}}parentId = {{[0-9]+}} : i32
// Each consumer CTA relays completion to its peer after consuming the load.
// CHECK: ttng.arrive_barrier {{.*}}shared_cluster_memory
// CHECK: ttng.arrive_barrier {{.*}}shared_cluster_memory
// CHECK: ttng.tc_gen5_mma
// Group 0: Descriptor load operations (producer)
// CHECK: partition0
// Cooperative loads map completion to the pair leader and relay it to the
// follower after the hardware CTA-group transaction completes.
// CHECK: ttng.wait_barrier {{.*}}channelGraph = array<i32: {{.*}}direction = "backward"{{.*}}dstTask = {{[0-9]+}} : i32{{.*}}parentId = {{[0-9]+}} : i32
// CHECK: nvg.cluster_id
// CHECK: ttng.map_to_remote
// CHECK: ttng.barrier_expect
// CHECK: ttng.async_tma_copy_global_to_local {{.*}}two_cta = true
// CHECK: ttng.wait_barrier {{.*}}channelGraph = array<i32: {{.*}}direction = "backward"{{.*}}dstTask = {{[0-9]+}} : i32{{.*}}parentId = {{[0-9]+}} : i32
// CHECK: ttng.barrier_expect
// CHECK: ttng.async_tma_copy_global_to_local {{.*}}two_cta = true
// Group 1: Epilogue operations
// CHECK: partition1
// CHECK: ttng.tmem_load
// CHECK: tt.descriptor_store

#blocked = #ttg.blocked<{sizePerThread = [1, 128], threadsPerWarp = [32, 1], warpsPerCTA = [4, 1], order = [0, 1]}>
#blocked1 = #ttg.blocked<{sizePerThread = [1, 1], threadsPerWarp = [1, 32], warpsPerCTA = [2, 2], order = [1, 0]}>
#blocked2 = #ttg.blocked<{sizePerThread = [1, 1], threadsPerWarp = [1, 32], warpsPerCTA = [1, 4], order = [1, 0]}>
#shared = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = false, elementBitWidth = 16}>
#shared1 = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = true, elementBitWidth = 16}>
#smem = #ttg.shared_memory
#tmem = #ttng.tensor_memory_encoding<blockM = 128, blockN = 128, colStride = 1>
module attributes {"ttg.cluster-dim-x" = 2 : i32, "ttg.cluster-dim-y" = 1 : i32, "ttg.cluster-dim-z" = 1 : i32, ttg.max_reg_auto_ws = 152 : i32, ttg.min_reg_auto_ws = 24 : i32, "ttg.num-ctas" = 1 : i32, "ttg.num-warps" = 4 : i32, ttg.target = "cuda:100", "ttg.threads-per-warp" = 32 : i32, "ttng.two-ctas" = true} {
  tt.func public @matmul_kernel_tma_ws(%a_desc: !tt.tensordesc<128x64xf16, #shared>, %a_desc_0: i32, %a_desc_1: i32, %a_desc_2: i64, %a_desc_3: i64, %b_desc: !tt.tensordesc<128x64xf16, #shared>, %b_desc_4: i32, %b_desc_5: i32, %b_desc_6: i64, %b_desc_7: i64, %c_desc: !tt.tensordesc<128x128xf16, #shared>, %c_desc_8: i32, %c_desc_9: i32, %c_desc_10: i64, %c_desc_11: i64, %M: i32 {tt.divisibility = 16 : i32}, %N: i32 {tt.divisibility = 16 : i32}, %K: i32 {tt.divisibility = 16 : i32}) attributes {noinline = false} {
    %accumulator = arith.constant false
    %true = arith.constant true
    %c8_i32 = arith.constant 8 : i32
    %c128_i32 = arith.constant 128 : i32
    %c64_i32 = arith.constant 64 : i32
    %c0_i32 = arith.constant 0 : i32
    %c1_i32 = arith.constant 1 : i32
    %c127_i32 = arith.constant 127 : i32
    %k_tiles = arith.constant 63 : i32
    %accumulator_12 = arith.constant dense<0.000000e+00> : tensor<128x128xf32, #blocked>
    %pid = tt.get_program_id x : i32
    %num_pid_m = arith.addi %M, %c127_i32 : i32
    %num_pid_m_13 = arith.divsi %num_pid_m, %c128_i32 : i32
    %num_pid_n = arith.addi %N, %c127_i32 : i32
    %num_pid_n_14 = arith.divsi %num_pid_n, %c128_i32 : i32
    %num_pid_in_group = arith.muli %num_pid_n_14, %c8_i32 : i32
    %group_id = arith.divsi %pid, %num_pid_in_group : i32
    %first_pid_m = arith.muli %group_id, %c8_i32 : i32
    %group_size_m = arith.subi %num_pid_m_13, %first_pid_m : i32
    %group_size_m_15 = arith.minsi %group_size_m, %c8_i32 : i32
    %pid_m = arith.remsi %pid, %group_size_m_15 : i32
    %pid_m_16 = arith.addi %first_pid_m, %pid_m : i32
    %pid_n = arith.remsi %pid, %num_pid_in_group : i32
    %pid_n_17 = arith.divsi %pid_n, %group_size_m_15 : i32
    %k_tiles_18 = arith.addi %K, %k_tiles : i32
    %k_tiles_19 = arith.divsi %k_tiles_18, %c64_i32 : i32
    %offs_am = arith.muli %pid_m_16, %c128_i32 : i32
    %offs_bn = arith.muli %pid_n_17, %c128_i32 : i32
    %accumulator_20, %accumulator_21 = ttng.tmem_alloc : () -> (!ttg.memdesc<128x128xf32, #tmem, #ttng.tensor_memory, mutable>, !ttg.async.token)
    %accumulator_23:2 = scf.for %accumulator_27 = %c0_i32 to %k_tiles_19 step %c1_i32 iter_args(%accumulator_28 = %accumulator, %accumulator_29 = %accumulator_21) -> (i1, !ttg.async.token)  : i32 {
      %offs_k = arith.muli %accumulator_27, %c64_i32 {loop.cluster = 1 : i32, loop.stage = 0 : i32} : i32
      %a = tt.descriptor_load %a_desc[%offs_am, %offs_k] {loop.cluster = 1 : i32, loop.stage = 0 : i32, ttg.partition = array<i32: 2>, two_cta_load} : !tt.tensordesc<128x64xf16, #shared> -> tensor<128x64xf16, #blocked1>
      %a_30 = ttg.local_alloc %a {loop.cluster = 0 : i32, loop.stage = 1 : i32, ttg.partition = array<i32: 2>} : (tensor<128x64xf16, #blocked1>) -> !ttg.memdesc<128x64xf16, #shared, #smem>
      %b = tt.descriptor_load %b_desc[%offs_bn, %offs_k] {loop.cluster = 1 : i32, loop.stage = 0 : i32, ttg.partition = array<i32: 2>, two_cta_load} : !tt.tensordesc<128x64xf16, #shared> -> tensor<128x64xf16, #blocked1>
      %accumulator_31 = ttg.local_alloc %b {loop.cluster = 0 : i32, loop.stage = 1 : i32, ttg.partition = array<i32: 2>} : (tensor<128x64xf16, #blocked1>) -> !ttg.memdesc<128x64xf16, #shared, #smem>
      %accumulator_32 = ttg.memdesc_trans %accumulator_31 {loop.cluster = 0 : i32, loop.stage = 1 : i32, order = array<i32: 1, 0>, ttg.partition = array<i32: 1>} : !ttg.memdesc<128x64xf16, #shared, #smem> -> !ttg.memdesc<64x128xf16, #shared1, #smem>
      %accumulator_33 = ttng.tc_gen5_mma %a_30, %accumulator_32, %accumulator_20[%accumulator_29], %accumulator_28, %true {loop.cluster = 0 : i32, loop.stage = 1 : i32, tt.self_latency = 1 : i32, ttg.partition = array<i32: 1>} : !ttg.memdesc<128x64xf16, #shared, #smem>, !ttg.memdesc<64x128xf16, #shared1, #smem>, !ttg.memdesc<128x128xf32, #tmem, #ttng.tensor_memory, mutable>
      scf.yield %true, %accumulator_33 : i1, !ttg.async.token
    } {tt.disallow_acc_multi_buffer, tt.scheduled_max_stage = 1 : i32, tt.warp_specialize, ttg.partition.stages = [0 : i32, 1 : i32, 0 : i32, 0 : i32], ttg.warp_specialize.tag = 0 : i32}
    %accumulator_24, %accumulator_25 = ttng.tmem_load %accumulator_20[%accumulator_23#1] {ttg.partition = array<i32: 3>} : !ttg.memdesc<128x128xf32, #tmem, #ttng.tensor_memory, mutable> -> tensor<128x128xf32, #blocked>
    %c = arith.truncf %accumulator_24 {ttg.partition = array<i32: 3>} : tensor<128x128xf32, #blocked> to tensor<128x128xf16, #blocked>
    %c_26 = ttg.convert_layout %c {ttg.partition = array<i32: 3>} : tensor<128x128xf16, #blocked> -> tensor<128x128xf16, #blocked2>
    tt.descriptor_store %c_desc[%offs_am, %offs_bn], %c_26 {ttg.partition = array<i32: 3>} : !tt.tensordesc<128x128xf16, #shared>, tensor<128x128xf16, #blocked2>
    tt.return
  }
}

// -----

// Test case: Persistent Blackwell GEMM kernel with nested loops.
// This IR represents a persistent GEMM kernel where:
// - The outer loop iterates over tiles (with step 148 for persistent scheduling)
// - The inner loop performs the K-dimension reduction
// - Partitions: 1 = MMA (transpose + mma), 2 = loads, 3 = epilogue store, 4 = Trunc + epilogue tmem load
// This tests that partition annotations are correctly tracked through nested control flow.

// CHECK-LABEL: @matmul_kernel_tma_persistent_ws
// CHECK: ttg.warp_specialize
// Default group (partition 0): MMA operations
// CHECK: default
// CHECK: ttng.tc_gen5_mma
// Partition 0 (partition 1): Descriptor load operations
// CHECK: partition0
// CHECK: ttng.async_tma_copy_global_to_local
// CHECK: ttng.async_tma_copy_global_to_local
// TODO: Partition 1 and Partition 2 should be merged by the
// partition scheduler?
// Partition 1 (partition 2): Epilogue store operations
// CHECK: partition1
// CHECK: tt.descriptor_store
// Partition 2 (partition 1): Epilogue load from tensor memory
// CHECK: partition2
// CHECK: ttng.tmem_load

#blocked9 = #ttg.blocked<{sizePerThread = [1, 128], threadsPerWarp = [32, 1], warpsPerCTA = [4, 1], order = [0, 1]}>
#blocked10 = #ttg.blocked<{sizePerThread = [1, 1], threadsPerWarp = [1, 32], warpsPerCTA = [1, 4], order = [1, 0]}>
#shared6 = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = false, elementBitWidth = 16}>
#shared7 = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = true, elementBitWidth = 16}>
#smem4 = #ttg.shared_memory
#tmem4 = #ttng.tensor_memory_encoding<blockM = 128, blockN = 128, colStride = 1>
module attributes {"ttg.cluster-dim-x" = 1 : i32, "ttg.cluster-dim-y" = 1 : i32, "ttg.cluster-dim-z" = 1 : i32, ttg.max_reg_auto_ws = 152 : i32, ttg.min_reg_auto_ws = 24 : i32, "ttg.num-ctas" = 1 : i32, "ttg.num-warps" = 4 : i32, ttg.target = "cuda:100", "ttg.threads-per-warp" = 32 : i32} {
  tt.func public @matmul_kernel_tma_persistent_ws(%a_desc: !tt.tensordesc<128x128xf16, #shared6>, %a_desc_0: i32, %a_desc_1: i32, %a_desc_2: i64, %a_desc_3: i64, %b_desc: !tt.tensordesc<128x128xf16, #shared6>, %b_desc_4: i32, %b_desc_5: i32, %b_desc_6: i64, %b_desc_7: i64, %c_desc: !tt.tensordesc<128x128xf16, #shared6>, %c_desc_8: i32, %c_desc_9: i32, %c_desc_10: i64, %c_desc_11: i64, %M: i32 {tt.divisibility = 16 : i32}, %N: i32 {tt.divisibility = 16 : i32}, %K: i32 {tt.divisibility = 16 : i32}) attributes {noinline = false} {
    %false = arith.constant false
    %true = arith.constant true
    %c148_i32 = arith.constant 148 : i32
    %c8_i32 = arith.constant 8 : i32
    %c128_i32 = arith.constant 128 : i32
    %c0_i32 = arith.constant 0 : i32
    %c1_i32 = arith.constant 1 : i32
    %c127_i32 = arith.constant 127 : i32
    %cst = arith.constant dense<0.000000e+00> : tensor<128x128xf32, #blocked9>
    %start_pid = tt.get_program_id x : i32
    %num_pid_m = arith.addi %M, %c127_i32 : i32
    %num_pid_m_12 = arith.divsi %num_pid_m, %c128_i32 : i32
    %num_pid_n = arith.addi %N, %c127_i32 : i32
    %num_pid_n_13 = arith.divsi %num_pid_n, %c128_i32 : i32
    %k_tiles = arith.addi %K, %c127_i32 : i32
    %k_tiles_14 = arith.divsi %k_tiles, %c128_i32 : i32
    %num_tiles = arith.muli %num_pid_m_12, %num_pid_n_13 : i32
    %tile_id_c = arith.subi %start_pid, %c148_i32 : i32
    %num_pid_in_group = arith.muli %num_pid_n_13, %c8_i32 : i32
    // Outer persistent loop - iterates over output tiles
    %tile_id_c_15 = scf.for %tile_id = %start_pid to %num_tiles step %c148_i32 iter_args(%tile_id_c_16 = %tile_id_c) -> (i32)  : i32 {
      %group_id = arith.divsi %tile_id, %num_pid_in_group : i32
      %first_pid_m = arith.muli %group_id, %c8_i32 : i32
      %group_size_m = arith.subi %num_pid_m_12, %first_pid_m : i32
      %group_size_m_17 = arith.minsi %group_size_m, %c8_i32 : i32
      %pid_m = arith.remsi %tile_id, %group_size_m_17 : i32
      %pid_m_18 = arith.addi %first_pid_m, %pid_m : i32
      %pid_n = arith.remsi %tile_id, %num_pid_in_group : i32
      %pid_n_19 = arith.divsi %pid_n, %group_size_m_17 : i32
      %offs_am = arith.muli %pid_m_18, %c128_i32 : i32
      %offs_bn = arith.muli %pid_n_19, %c128_i32 : i32
      %accumulator, %accumulator_20 = ttng.tmem_alloc : () -> (!ttg.memdesc<128x128xf32, #tmem4, #ttng.tensor_memory, mutable>, !ttg.async.token)
      %accumulator_21 = ttng.tmem_store %cst, %accumulator[%accumulator_20], %true : tensor<128x128xf32, #blocked9> -> !ttg.memdesc<128x128xf32, #tmem4, #ttng.tensor_memory, mutable>
      // Inner K-loop with partition annotations
      %accumulator_22:2 = scf.for %accumulator_36 = %c0_i32 to %k_tiles_14 step %c1_i32 iter_args(%arg21 = %false, %accumulator_37 = %accumulator_21) -> (i1, !ttg.async.token)  : i32 {
        %offs_k = arith.muli %accumulator_36, %c128_i32 {loop.cluster = 2 : i32, loop.stage = 0 : i32} : i32
        // Partition 2: Load operations
        %a = tt.descriptor_load %a_desc[%offs_am, %offs_k] {loop.cluster = 2 : i32, loop.stage = 0 : i32, ttg.partition = array<i32: 2>} : !tt.tensordesc<128x128xf16, #shared6> -> tensor<128x128xf16, #blocked10>
        %a_38 = ttg.local_alloc %a {loop.cluster = 0 : i32, loop.stage = 2 : i32, ttg.partition = array<i32: 2>} : (tensor<128x128xf16, #blocked10>) -> !ttg.memdesc<128x128xf16, #shared6, #smem4>
        %b = tt.descriptor_load %b_desc[%offs_bn, %offs_k] {loop.cluster = 2 : i32, loop.stage = 0 : i32, ttg.partition = array<i32: 2>} : !tt.tensordesc<128x128xf16, #shared6> -> tensor<128x128xf16, #blocked10>
        %accumulator_39 = ttg.local_alloc %b {loop.cluster = 0 : i32, loop.stage = 2 : i32, ttg.partition = array<i32: 2>} : (tensor<128x128xf16, #blocked10>) -> !ttg.memdesc<128x128xf16, #shared6, #smem4>
        // Partition 1: Transpose + MMA operations
        %accumulator_40 = ttg.memdesc_trans %accumulator_39 {loop.cluster = 0 : i32, loop.stage = 2 : i32, order = array<i32: 1, 0>, ttg.partition = array<i32: 1>} : !ttg.memdesc<128x128xf16, #shared6, #smem4> -> !ttg.memdesc<128x128xf16, #shared7, #smem4>
        %accumulator_41 = ttng.tc_gen5_mma %a_38, %accumulator_40, %accumulator[%accumulator_37], %arg21, %true {loop.cluster = 0 : i32, loop.stage = 2 : i32, tt.self_latency = 1 : i32, ttg.partition = array<i32: 1>} : !ttg.memdesc<128x128xf16, #shared6, #smem4>, !ttg.memdesc<128x128xf16, #shared7, #smem4>, !ttg.memdesc<128x128xf32, #tmem4, #ttng.tensor_memory, mutable>
        scf.yield %true, %accumulator_41 : i1, !ttg.async.token
      } {tt.scheduled_max_stage = 2 : i32}
      // Epilogue: compute next tile coordinates
      %tile_id_c_23 = arith.addi %tile_id_c_16, %c148_i32 : i32
      %group_id_24 = arith.divsi %tile_id_c_23, %num_pid_in_group : i32
      %first_pid_m_25 = arith.muli %group_id_24, %c8_i32 : i32
      %group_size_m_26 = arith.subi %num_pid_m_12, %first_pid_m_25 : i32
      %group_size_m_27 = arith.minsi %group_size_m_26, %c8_i32 : i32
      %pid_m_28 = arith.remsi %tile_id_c_23, %group_size_m_27 : i32
      %pid_m_29 = arith.addi %first_pid_m_25, %pid_m_28 : i32
      %pid_n_30 = arith.remsi %tile_id_c_23, %num_pid_in_group : i32
      %pid_n_31 = arith.divsi %pid_n_30, %group_size_m_27 : i32
      %offs_am_c = arith.muli %pid_m_29, %c128_i32 : i32
      %offs_bn_c = arith.muli %pid_n_31, %c128_i32 : i32
      // Partition 4: Load from tensor memory
      %accumulator_32, %accumulator_33 = ttng.tmem_load %accumulator[%accumulator_22#1] {ttg.partition = array<i32: 4>} : !ttg.memdesc<128x128xf32, #tmem4, #ttng.tensor_memory, mutable> -> tensor<128x128xf32, #blocked9>
      %accumulator_34 = arith.truncf %accumulator_32 {ttg.partition = array<i32: 4>} : tensor<128x128xf32, #blocked9> to tensor<128x128xf16, #blocked9>
      // Partition 3: Store to global memory
      %accumulator_35 = ttg.convert_layout %accumulator_34 {ttg.partition = array<i32: 3>} : tensor<128x128xf16, #blocked9> -> tensor<128x128xf16, #blocked10>
      tt.descriptor_store %c_desc[%offs_am_c, %offs_bn_c], %accumulator_35 {ttg.partition = array<i32: 3>} : !tt.tensordesc<128x128xf16, #shared6>, tensor<128x128xf16, #blocked10>
      scf.yield %tile_id_c_23 : i32
    } {tt.disallow_acc_multi_buffer, tt.warp_specialize, ttg.partition.stages = [0 : i32, 1 : i32, 0 : i32, 0 : i32, 0 : i32], ttg.warp_specialize.tag = 0 : i32}
    tt.return
  }
}

// -----

// Test case: Blackwell matrix multiplication with explicit tmem_store before loop.
// This IR includes ttng.tmem_store to initialize the accumulator before the loop.

// CHECK-LABEL: @matmul_kernel_tma_ws_with_tmem_store
// CHECK: ttg.warp_specialize
// Default group: MMA operations
// CHECK: default
// CHECK: ttng.tmem_store
// CHECK: ttng.tc_gen5_mma
// Group 0: Descriptor load operations (producer)
// CHECK: partition0
// CHECK: ttng.async_tma_copy_global_to_local
// CHECK: ttng.async_tma_copy_global_to_local
// Group 1: Epilogue operations
// CHECK: partition1
// CHECK: ttng.tmem_load
// CHECK: tt.descriptor_store

#blocked3 = #ttg.blocked<{sizePerThread = [1, 128], threadsPerWarp = [32, 1], warpsPerCTA = [4, 1], order = [0, 1]}>
#blocked4 = #ttg.blocked<{sizePerThread = [1, 1], threadsPerWarp = [1, 32], warpsPerCTA = [2, 2], order = [1, 0]}>
#blocked5 = #ttg.blocked<{sizePerThread = [1, 1], threadsPerWarp = [1, 32], warpsPerCTA = [1, 4], order = [1, 0]}>
#shared2 = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = false, elementBitWidth = 16}>
#shared3 = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = true, elementBitWidth = 16}>
#smem2 = #ttg.shared_memory
#tmem2 = #ttng.tensor_memory_encoding<blockM = 128, blockN = 128, colStride = 1>
module attributes {"ttg.cluster-dim-x" = 1 : i32, "ttg.cluster-dim-y" = 1 : i32, "ttg.cluster-dim-z" = 1 : i32, ttg.max_reg_auto_ws = 152 : i32, ttg.min_reg_auto_ws = 24 : i32, "ttg.num-ctas" = 1 : i32, "ttg.num-warps" = 4 : i32, ttg.target = "cuda:100", "ttg.threads-per-warp" = 32 : i32} {
  tt.func public @matmul_kernel_tma_ws_with_tmem_store(%a_desc: !tt.tensordesc<128x64xf16, #shared2>, %a_desc_0: i32, %a_desc_1: i32, %a_desc_2: i64, %a_desc_3: i64, %b_desc: !tt.tensordesc<128x64xf16, #shared2>, %b_desc_4: i32, %b_desc_5: i32, %b_desc_6: i64, %b_desc_7: i64, %c_desc: !tt.tensordesc<128x128xf16, #shared2>, %c_desc_8: i32, %c_desc_9: i32, %c_desc_10: i64, %c_desc_11: i64, %M: i32 {tt.divisibility = 16 : i32}, %N: i32 {tt.divisibility = 16 : i32}, %K: i32 {tt.divisibility = 16 : i32}) attributes {noinline = false} {
    %accumulator = arith.constant false
    %true = arith.constant true
    %c8_i32 = arith.constant 8 : i32
    %c128_i32 = arith.constant 128 : i32
    %c64_i32 = arith.constant 64 : i32
    %c0_i32 = arith.constant 0 : i32
    %c1_i32 = arith.constant 1 : i32
    %c127_i32 = arith.constant 127 : i32
    %k_tiles = arith.constant 63 : i32
    %accumulator_12 = arith.constant dense<0.000000e+00> : tensor<128x128xf32, #blocked3>
    %pid = tt.get_program_id x : i32
    %num_pid_m = arith.addi %M, %c127_i32 : i32
    %num_pid_m_13 = arith.divsi %num_pid_m, %c128_i32 : i32
    %num_pid_n = arith.addi %N, %c127_i32 : i32
    %num_pid_n_14 = arith.divsi %num_pid_n, %c128_i32 : i32
    %num_pid_in_group = arith.muli %num_pid_n_14, %c8_i32 : i32
    %group_id = arith.divsi %pid, %num_pid_in_group : i32
    %first_pid_m = arith.muli %group_id, %c8_i32 : i32
    %group_size_m = arith.subi %num_pid_m_13, %first_pid_m : i32
    %group_size_m_15 = arith.minsi %group_size_m, %c8_i32 : i32
    %pid_m = arith.remsi %pid, %group_size_m_15 : i32
    %pid_m_16 = arith.addi %first_pid_m, %pid_m : i32
    %pid_n = arith.remsi %pid, %num_pid_in_group : i32
    %pid_n_17 = arith.divsi %pid_n, %group_size_m_15 : i32
    %k_tiles_18 = arith.addi %K, %k_tiles : i32
    %k_tiles_19 = arith.divsi %k_tiles_18, %c64_i32 : i32
    %offs_am = arith.muli %pid_m_16, %c128_i32 : i32
    %offs_bn = arith.muli %pid_n_17, %c128_i32 : i32
    %accumulator_20, %accumulator_21 = ttng.tmem_alloc : () -> (!ttg.memdesc<128x128xf32, #tmem2, #ttng.tensor_memory, mutable>, !ttg.async.token)
    %accumulator_22 = ttng.tmem_store %accumulator_12, %accumulator_20[%accumulator_21], %true : tensor<128x128xf32, #blocked3> -> !ttg.memdesc<128x128xf32, #tmem2, #ttng.tensor_memory, mutable>
    %accumulator_23:2 = scf.for %accumulator_27 = %c0_i32 to %k_tiles_19 step %c1_i32 iter_args(%accumulator_28 = %accumulator, %accumulator_29 = %accumulator_22) -> (i1, !ttg.async.token)  : i32 {
      %offs_k = arith.muli %accumulator_27, %c64_i32 {loop.cluster = 1 : i32, loop.stage = 0 : i32} : i32
      %a = tt.descriptor_load %a_desc[%offs_am, %offs_k] {loop.cluster = 1 : i32, loop.stage = 0 : i32, ttg.partition = array<i32: 2>} : !tt.tensordesc<128x64xf16, #shared2> -> tensor<128x64xf16, #blocked4>
      %a_30 = ttg.local_alloc %a {loop.cluster = 0 : i32, loop.stage = 1 : i32, ttg.partition = array<i32: 2>} : (tensor<128x64xf16, #blocked4>) -> !ttg.memdesc<128x64xf16, #shared2, #smem2>
      %b = tt.descriptor_load %b_desc[%offs_bn, %offs_k] {loop.cluster = 1 : i32, loop.stage = 0 : i32, ttg.partition = array<i32: 2>} : !tt.tensordesc<128x64xf16, #shared2> -> tensor<128x64xf16, #blocked4>
      %accumulator_31 = ttg.local_alloc %b {loop.cluster = 0 : i32, loop.stage = 1 : i32, ttg.partition = array<i32: 2>} : (tensor<128x64xf16, #blocked4>) -> !ttg.memdesc<128x64xf16, #shared2, #smem2>
      %accumulator_32 = ttg.memdesc_trans %accumulator_31 {loop.cluster = 0 : i32, loop.stage = 1 : i32, order = array<i32: 1, 0>, ttg.partition = array<i32: 1>} : !ttg.memdesc<128x64xf16, #shared2, #smem2> -> !ttg.memdesc<64x128xf16, #shared3, #smem2>
      %accumulator_33 = ttng.tc_gen5_mma %a_30, %accumulator_32, %accumulator_20[%accumulator_29], %accumulator_28, %true {loop.cluster = 0 : i32, loop.stage = 1 : i32, tt.self_latency = 1 : i32, ttg.partition = array<i32: 1>} : !ttg.memdesc<128x64xf16, #shared2, #smem2>, !ttg.memdesc<64x128xf16, #shared3, #smem2>, !ttg.memdesc<128x128xf32, #tmem2, #ttng.tensor_memory, mutable>
      scf.yield %true, %accumulator_33 : i1, !ttg.async.token
    } {tt.disallow_acc_multi_buffer, tt.scheduled_max_stage = 1 : i32, tt.warp_specialize, ttg.partition.stages = [0 : i32, 1 : i32, 0 : i32, 0 : i32], ttg.warp_specialize.tag = 0 : i32}
    %accumulator_24, %accumulator_25 = ttng.tmem_load %accumulator_20[%accumulator_23#1] {ttg.partition = array<i32: 3>} : !ttg.memdesc<128x128xf32, #tmem2, #ttng.tensor_memory, mutable> -> tensor<128x128xf32, #blocked3>
    %c = arith.truncf %accumulator_24 {ttg.partition = array<i32: 3>} : tensor<128x128xf32, #blocked3> to tensor<128x128xf16, #blocked3>
    %c_26 = ttg.convert_layout %c {ttg.partition = array<i32: 3>} : tensor<128x128xf16, #blocked3> -> tensor<128x128xf16, #blocked5>
    tt.descriptor_store %c_desc[%offs_am, %offs_bn], %c_26 {ttg.partition = array<i32: 3>} : !tt.tensordesc<128x128xf16, #shared2>, tensor<128x128xf16, #blocked5>
    tt.return
  }
}

// -----

// Test case: Blackwell matrix multiplication with operand D initialization in partition 3.
// The initial accumulator value is in partition 3 (different from MMA partition 1).
// The tmem_store should get partition 3 propagated to it from its source value.

// CHECK-LABEL: @matmul_kernel_operand_d_init_partition
// CHECK: ttg.warp_specialize
// Default group: MMA operations with tmem_store
// CHECK: default
// CHECK: ttng.tc_gen5_mma
// Group 0: Descriptor load operations (producer)
// CHECK: partition0
// CHECK: ttng.async_tma_copy_global_to_local
// CHECK: ttng.async_tma_copy_global_to_local
// Group 1: Epilogue operations (includes accumulator init - partition 3)
// CHECK: partition1
// The tmem_store should inherit the partition from its source value
// CHECK: ttng.tmem_store
// CHECK: ttng.tmem_load
// CHECK: tt.descriptor_store

#blocked6 = #ttg.blocked<{sizePerThread = [1, 128], threadsPerWarp = [32, 1], warpsPerCTA = [4, 1], order = [0, 1]}>
#blocked7 = #ttg.blocked<{sizePerThread = [1, 1], threadsPerWarp = [1, 32], warpsPerCTA = [2, 2], order = [1, 0]}>
#blocked8 = #ttg.blocked<{sizePerThread = [1, 1], threadsPerWarp = [1, 32], warpsPerCTA = [1, 4], order = [1, 0]}>
#shared4 = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = false, elementBitWidth = 16}>
#shared5 = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = true, elementBitWidth = 16}>
#smem3 = #ttg.shared_memory
#tmem3 = #ttng.tensor_memory_encoding<blockM = 128, blockN = 128, colStride = 1>
module attributes {"ttg.cluster-dim-x" = 1 : i32, "ttg.cluster-dim-y" = 1 : i32, "ttg.cluster-dim-z" = 1 : i32, ttg.max_reg_auto_ws = 152 : i32, ttg.min_reg_auto_ws = 24 : i32, "ttg.num-ctas" = 1 : i32, "ttg.num-warps" = 4 : i32, ttg.target = "cuda:100", "ttg.threads-per-warp" = 32 : i32} {
  tt.func public @matmul_kernel_operand_d_init_partition(%a_desc: !tt.tensordesc<128x64xf16, #shared4>, %a_desc_0: i32, %a_desc_1: i32, %a_desc_2: i64, %a_desc_3: i64, %b_desc: !tt.tensordesc<128x64xf16, #shared4>, %b_desc_4: i32, %b_desc_5: i32, %b_desc_6: i64, %b_desc_7: i64, %c_desc: !tt.tensordesc<128x128xf16, #shared4>, %c_desc_8: i32, %c_desc_9: i32, %c_desc_10: i64, %c_desc_11: i64, %M: i32 {tt.divisibility = 16 : i32}, %N: i32 {tt.divisibility = 16 : i32}, %K: i32 {tt.divisibility = 16 : i32}) attributes {noinline = false} {
    %accumulator = arith.constant false
    %true = arith.constant true
    %c8_i32 = arith.constant 8 : i32
    %c128_i32 = arith.constant 128 : i32
    %c64_i32 = arith.constant 64 : i32
    %c0_i32 = arith.constant 0 : i32
    %c1_i32 = arith.constant 1 : i32
    %c127_i32 = arith.constant 127 : i32
    %k_tiles = arith.constant 63 : i32
    // Initial accumulator value is in partition 3 - tmem_store should inherit this
    %accumulator_12 = arith.constant {ttg.partition = array<i32: 3>} dense<0.000000e+00> : tensor<128x128xf32, #blocked6>
    %pid = tt.get_program_id x : i32
    %num_pid_m = arith.addi %M, %c127_i32 : i32
    %num_pid_m_13 = arith.divsi %num_pid_m, %c128_i32 : i32
    %num_pid_n = arith.addi %N, %c127_i32 : i32
    %num_pid_n_14 = arith.divsi %num_pid_n, %c128_i32 : i32
    %num_pid_in_group = arith.muli %num_pid_n_14, %c8_i32 : i32
    %group_id = arith.divsi %pid, %num_pid_in_group : i32
    %first_pid_m = arith.muli %group_id, %c8_i32 : i32
    %group_size_m = arith.subi %num_pid_m_13, %first_pid_m : i32
    %group_size_m_15 = arith.minsi %group_size_m, %c8_i32 : i32
    %pid_m = arith.remsi %pid, %group_size_m_15 : i32
    %pid_m_16 = arith.addi %first_pid_m, %pid_m : i32
    %pid_n = arith.remsi %pid, %num_pid_in_group : i32
    %pid_n_17 = arith.divsi %pid_n, %group_size_m_15 : i32
    %k_tiles_18 = arith.addi %K, %k_tiles : i32
    %k_tiles_19 = arith.divsi %k_tiles_18, %c64_i32 : i32
    %offs_am = arith.muli %pid_m_16, %c128_i32 : i32
    %offs_bn = arith.muli %pid_n_17, %c128_i32 : i32
    %accumulator_20, %accumulator_21 = ttng.tmem_alloc : () -> (!ttg.memdesc<128x128xf32, #tmem3, #ttng.tensor_memory, mutable>, !ttg.async.token)
    // tmem_store should get partition 3 from accumulator_12 source
    %accumulator_22 = ttng.tmem_store %accumulator_12, %accumulator_20[%accumulator_21], %true : tensor<128x128xf32, #blocked6> -> !ttg.memdesc<128x128xf32, #tmem3, #ttng.tensor_memory, mutable>
    %accumulator_23:2 = scf.for %accumulator_27 = %c0_i32 to %k_tiles_19 step %c1_i32 iter_args(%accumulator_28 = %accumulator, %accumulator_29 = %accumulator_22) -> (i1, !ttg.async.token)  : i32 {
      %offs_k = arith.muli %accumulator_27, %c64_i32 {loop.cluster = 1 : i32, loop.stage = 0 : i32} : i32
      %a = tt.descriptor_load %a_desc[%offs_am, %offs_k] {loop.cluster = 1 : i32, loop.stage = 0 : i32, ttg.partition = array<i32: 2>} : !tt.tensordesc<128x64xf16, #shared4> -> tensor<128x64xf16, #blocked7>
      %a_30 = ttg.local_alloc %a {loop.cluster = 0 : i32, loop.stage = 1 : i32, ttg.partition = array<i32: 2>} : (tensor<128x64xf16, #blocked7>) -> !ttg.memdesc<128x64xf16, #shared4, #smem3>
      %b = tt.descriptor_load %b_desc[%offs_bn, %offs_k] {loop.cluster = 1 : i32, loop.stage = 0 : i32, ttg.partition = array<i32: 2>} : !tt.tensordesc<128x64xf16, #shared4> -> tensor<128x64xf16, #blocked7>
      %accumulator_31 = ttg.local_alloc %b {loop.cluster = 0 : i32, loop.stage = 1 : i32, ttg.partition = array<i32: 2>} : (tensor<128x64xf16, #blocked7>) -> !ttg.memdesc<128x64xf16, #shared4, #smem3>
      %accumulator_32 = ttg.memdesc_trans %accumulator_31 {loop.cluster = 0 : i32, loop.stage = 1 : i32, order = array<i32: 1, 0>, ttg.partition = array<i32: 1>} : !ttg.memdesc<128x64xf16, #shared4, #smem3> -> !ttg.memdesc<64x128xf16, #shared5, #smem3>
      // MMA is in partition 1
      %accumulator_33 = ttng.tc_gen5_mma %a_30, %accumulator_32, %accumulator_20[%accumulator_29], %accumulator_28, %true {loop.cluster = 0 : i32, loop.stage = 1 : i32, tt.self_latency = 1 : i32, ttg.partition = array<i32: 1>} : !ttg.memdesc<128x64xf16, #shared4, #smem3>, !ttg.memdesc<64x128xf16, #shared5, #smem3>, !ttg.memdesc<128x128xf32, #tmem3, #ttng.tensor_memory, mutable>
      scf.yield %true, %accumulator_33 : i1, !ttg.async.token
    } {tt.disallow_acc_multi_buffer, tt.scheduled_max_stage = 1 : i32, tt.warp_specialize, ttg.partition.stages = [0 : i32, 1 : i32, 0 : i32, 0 : i32], ttg.warp_specialize.tag = 0 : i32}
    %accumulator_24, %accumulator_25 = ttng.tmem_load %accumulator_20[%accumulator_23#1] {ttg.partition = array<i32: 3>} : !ttg.memdesc<128x128xf32, #tmem3, #ttng.tensor_memory, mutable> -> tensor<128x128xf32, #blocked6>
    %c = arith.truncf %accumulator_24 {ttg.partition = array<i32: 3>} : tensor<128x128xf32, #blocked6> to tensor<128x128xf16, #blocked6>
    %c_26 = ttg.convert_layout %c {ttg.partition = array<i32: 3>} : tensor<128x128xf16, #blocked6> -> tensor<128x128xf16, #blocked8>
    tt.descriptor_store %c_desc[%offs_am, %offs_bn], %c_26 {ttg.partition = array<i32: 3>} : !tt.tensordesc<128x128xf16, #shared4>, tensor<128x128xf16, #blocked8>
    tt.return
  }
}

// -----

// Test case: Persistent Blackwell GEMM kernel with early-lowered TMA store.
// Same as the persistent test above, but tt.descriptor_store has been lowered
// (by WSTMAStoreLowering) into:
//   convert_layout -> local_alloc -> fence_async_shared ->
//   async_tma_copy_local_to_global -> async_tma_store_token_wait
// Partitions: 1 = MMA, 2 = loads, 3 = TMA store, 4 = tmem_load + truncf + convert + alloc
// The WS pass should fuse the consumer release barrier into the
// TMAStoreTokenWaitOp instead of emitting a separate arrive_barrier.

// CHECK-LABEL: @matmul_kernel_tma_persistent_early_store
// CHECK: ttg.warp_specialize
// Default group: MMA operations
// CHECK: default
// CHECK: ttng.tc_gen5_mma
// Partition 0: Descriptor load operations (producer)
// CHECK: partition0
// CHECK: ttng.async_tma_copy_global_to_local
// CHECK: ttng.async_tma_copy_global_to_local
// Partition 1: Early-lowered TMA store
// CHECK: partition1
// CHECK: ttng.async_tma_copy_local_to_global
// Barrier should be fused into the wait op, not a separate arrive_barrier
// CHECK: ttng.async_tma_store_token_wait %{{.*}}, %{{.*}}[%{{.*}}]
// Partition 2: Epilogue load from tensor memory
// CHECK: partition2
// CHECK: ttng.tmem_load

#blocked11 = #ttg.blocked<{sizePerThread = [1, 128], threadsPerWarp = [32, 1], warpsPerCTA = [4, 1], order = [0, 1]}>
#blocked12 = #ttg.blocked<{sizePerThread = [1, 1], threadsPerWarp = [1, 32], warpsPerCTA = [1, 4], order = [1, 0]}>
#shared8 = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = false, elementBitWidth = 16}>
#shared9 = #ttg.nvmma_shared<{swizzlingByteWidth = 128, transposed = true, elementBitWidth = 16}>
#smem5 = #ttg.shared_memory
#tmem5 = #ttng.tensor_memory_encoding<blockM = 128, blockN = 128, colStride = 1>
module attributes {"ttg.cluster-dim-x" = 1 : i32, "ttg.cluster-dim-y" = 1 : i32, "ttg.cluster-dim-z" = 1 : i32, ttg.max_reg_auto_ws = 152 : i32, ttg.min_reg_auto_ws = 24 : i32, "ttg.num-ctas" = 1 : i32, "ttg.num-warps" = 4 : i32, ttg.target = "cuda:100", "ttg.threads-per-warp" = 32 : i32} {
  tt.func public @matmul_kernel_tma_persistent_early_store(%a_desc: !tt.tensordesc<128x128xf16, #shared8>, %a_desc_0: i32, %a_desc_1: i32, %a_desc_2: i64, %a_desc_3: i64, %b_desc: !tt.tensordesc<128x128xf16, #shared8>, %b_desc_4: i32, %b_desc_5: i32, %b_desc_6: i64, %b_desc_7: i64, %c_desc: !tt.tensordesc<128x128xf16, #shared8>, %c_desc_8: i32, %c_desc_9: i32, %c_desc_10: i64, %c_desc_11: i64, %M: i32 {tt.divisibility = 16 : i32}, %N: i32 {tt.divisibility = 16 : i32}, %K: i32 {tt.divisibility = 16 : i32}) attributes {noinline = false} {
    %false = arith.constant false
    %true = arith.constant true
    %c148_i32 = arith.constant 148 : i32
    %c8_i32 = arith.constant 8 : i32
    %c128_i32 = arith.constant 128 : i32
    %c0_i32 = arith.constant 0 : i32
    %c1_i32 = arith.constant 1 : i32
    %c127_i32 = arith.constant 127 : i32
    %cst = arith.constant dense<0.000000e+00> : tensor<128x128xf32, #blocked11>
    %start_pid = tt.get_program_id x : i32
    %num_pid_m = arith.addi %M, %c127_i32 : i32
    %num_pid_m_12 = arith.divsi %num_pid_m, %c128_i32 : i32
    %num_pid_n = arith.addi %N, %c127_i32 : i32
    %num_pid_n_13 = arith.divsi %num_pid_n, %c128_i32 : i32
    %k_tiles = arith.addi %K, %c127_i32 : i32
    %k_tiles_14 = arith.divsi %k_tiles, %c128_i32 : i32
    %num_tiles = arith.muli %num_pid_m_12, %num_pid_n_13 : i32
    %tile_id_c = arith.subi %start_pid, %c148_i32 : i32
    %num_pid_in_group = arith.muli %num_pid_n_13, %c8_i32 : i32
    // Outer persistent loop
    %tile_id_c_15 = scf.for %tile_id = %start_pid to %num_tiles step %c148_i32 iter_args(%tile_id_c_16 = %tile_id_c) -> (i32)  : i32 {
      %group_id = arith.divsi %tile_id, %num_pid_in_group : i32
      %first_pid_m = arith.muli %group_id, %c8_i32 : i32
      %group_size_m = arith.subi %num_pid_m_12, %first_pid_m : i32
      %group_size_m_17 = arith.minsi %group_size_m, %c8_i32 : i32
      %pid_m = arith.remsi %tile_id, %group_size_m_17 : i32
      %pid_m_18 = arith.addi %first_pid_m, %pid_m : i32
      %pid_n = arith.remsi %tile_id, %num_pid_in_group : i32
      %pid_n_19 = arith.divsi %pid_n, %group_size_m_17 : i32
      %offs_am = arith.muli %pid_m_18, %c128_i32 : i32
      %offs_bn = arith.muli %pid_n_19, %c128_i32 : i32
      %accumulator, %accumulator_20 = ttng.tmem_alloc : () -> (!ttg.memdesc<128x128xf32, #tmem5, #ttng.tensor_memory, mutable>, !ttg.async.token)
      %accumulator_21 = ttng.tmem_store %cst, %accumulator[%accumulator_20], %true : tensor<128x128xf32, #blocked11> -> !ttg.memdesc<128x128xf32, #tmem5, #ttng.tensor_memory, mutable>
      // Inner K-loop with partition annotations
      %accumulator_22:2 = scf.for %i = %c0_i32 to %k_tiles_14 step %c1_i32 iter_args(%arg21 = %false, %accumulator_37 = %accumulator_21) -> (i1, !ttg.async.token)  : i32 {
        %offs_k = arith.muli %i, %c128_i32 {loop.cluster = 2 : i32, loop.stage = 0 : i32, ttg.partition = array<i32: 2>} : i32
        // Partition 2: Load operations
        %a = tt.descriptor_load %a_desc[%offs_am, %offs_k] {loop.cluster = 2 : i32, loop.stage = 0 : i32, ttg.partition = array<i32: 2>} : !tt.tensordesc<128x128xf16, #shared8> -> tensor<128x128xf16, #blocked12>
        %a_alloc = ttg.local_alloc %a {loop.cluster = 0 : i32, loop.stage = 2 : i32, ttg.partition = array<i32: 2>} : (tensor<128x128xf16, #blocked12>) -> !ttg.memdesc<128x128xf16, #shared8, #smem5>
        %b = tt.descriptor_load %b_desc[%offs_bn, %offs_k] {loop.cluster = 2 : i32, loop.stage = 0 : i32, ttg.partition = array<i32: 2>} : !tt.tensordesc<128x128xf16, #shared8> -> tensor<128x128xf16, #blocked12>
        %b_alloc = ttg.local_alloc %b {loop.cluster = 0 : i32, loop.stage = 2 : i32, ttg.partition = array<i32: 2>} : (tensor<128x128xf16, #blocked12>) -> !ttg.memdesc<128x128xf16, #shared8, #smem5>
        // Partition 1: Transpose + MMA operations
        %b_trans = ttg.memdesc_trans %b_alloc {loop.cluster = 0 : i32, loop.stage = 2 : i32, order = array<i32: 1, 0>, ttg.partition = array<i32: 1>} : !ttg.memdesc<128x128xf16, #shared8, #smem5> -> !ttg.memdesc<128x128xf16, #shared9, #smem5>
        %mma_token = ttng.tc_gen5_mma %a_alloc, %b_trans, %accumulator[%accumulator_37], %arg21, %true {loop.cluster = 0 : i32, loop.stage = 2 : i32, tt.self_latency = 1 : i32, ttg.partition = array<i32: 1>} : !ttg.memdesc<128x128xf16, #shared8, #smem5>, !ttg.memdesc<128x128xf16, #shared9, #smem5>, !ttg.memdesc<128x128xf32, #tmem5, #ttng.tensor_memory, mutable>
        scf.yield %true, %mma_token : i1, !ttg.async.token
      } {tt.scheduled_max_stage = 2 : i32, ttg.partition = array<i32: 4>}
      // Epilogue: compute next tile coordinates
      %tile_id_c_23 = arith.addi %tile_id_c_16, %c148_i32 : i32
      %group_id_24 = arith.divsi %tile_id_c_23, %num_pid_in_group : i32
      %first_pid_m_25 = arith.muli %group_id_24, %c8_i32 : i32
      %group_size_m_26 = arith.subi %num_pid_m_12, %first_pid_m_25 : i32
      %group_size_m_27 = arith.minsi %group_size_m_26, %c8_i32 : i32
      %pid_m_28 = arith.remsi %tile_id_c_23, %group_size_m_27 : i32
      %pid_m_29 = arith.addi %first_pid_m_25, %pid_m_28 : i32
      %pid_n_30 = arith.remsi %tile_id_c_23, %num_pid_in_group : i32
      %pid_n_31 = arith.divsi %pid_n_30, %group_size_m_27 : i32
      %offs_am_c = arith.muli %pid_m_29, %c128_i32 : i32
      %offs_bn_c = arith.muli %pid_n_31, %c128_i32 : i32
      // Partition 4: Load from tensor memory and prepare for store
      %tmem_result, %tmem_token = ttng.tmem_load %accumulator[%accumulator_22#1] {ttg.partition = array<i32: 4>} : !ttg.memdesc<128x128xf32, #tmem5, #ttng.tensor_memory, mutable> -> tensor<128x128xf32, #blocked11>
      %truncated = arith.truncf %tmem_result {ttg.partition = array<i32: 4>} : tensor<128x128xf32, #blocked11> to tensor<128x128xf16, #blocked11>
      %converted = ttg.convert_layout %truncated {ttg.partition = array<i32: 4>} : tensor<128x128xf16, #blocked11> -> tensor<128x128xf16, #blocked12>
      %store_alloc = ttg.local_alloc %converted {ttg.partition = array<i32: 4>} : (tensor<128x128xf16, #blocked12>) -> !ttg.memdesc<128x128xf16, #shared8, #smem5, mutable>
      ttng.fence_async_shared {bCluster = false}
      // Partition 3: Async TMA store
      %store_token = ttng.async_tma_copy_local_to_global %c_desc[%offs_am_c, %offs_bn_c] %store_alloc {ttg.partition = array<i32: 3>} : !tt.tensordesc<128x128xf16, #shared8>, !ttg.memdesc<128x128xf16, #shared8, #smem5, mutable> -> !ttg.async.token
      ttng.async_tma_store_token_wait %store_token {ttg.partition = array<i32: 3>} : !ttg.async.token
      scf.yield %tile_id_c_23 : i32
    } {tt.data_partition_factor = 1 : i32, tt.warp_specialize, ttg.partition.stages = [0 : i32, 1 : i32, 0 : i32, 0 : i32, 0 : i32], ttg.warp_specialize.tag = 0 : i32}
    tt.return
  }
}
